import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "com.github.hierynomus.license-report"
  id "com.github.johnrengelman.shadow"
  id "de.undercouch.download"
  id "org.cyclonedx.bom"
}

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Application"
  }
}

configurations {
  zip
  jsw
  scanner
  web
  shutdowner
  jdbc_mssql {
    transitive = false
  }
  jdbc_postgresql {
    transitive = false
  }
  jdbc_h2 {
    transitive = false
  }
  bundledPlugin {
    transitive = false
  }
  bundledPlugin_deps {
    extendsFrom bundledPlugin
    transitive = true
  }
  appLicenses.extendsFrom(compile, web, scanner, jsw, jdbc_mssql, jdbc_postgresql, jdbc_h2)
  cyclonedx
}

jar.enabled = false
shadowJar {
  archiveBaseName = 'sonar-application'
  archiveClassifier = null
  mergeServiceFiles()
  manifest {
    attributes('Main-Class': 'org.sonar.application.App')
  }
}

dependencies {
    // please keep list ordered
    compile 'org.slf4j:slf4j-api'

    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client'
    compile 'org.sonarsource.api.plugin:sonar-plugin-api'
    compile project(':server:sonar-ce')
    compile project(':server:sonar-main')
    compile project(':server:sonar-process')
    compile project(':server:sonar-webserver')
    compile project(':sonar-core')
    compile project(':sonar-plugin-api-impl')

    compileOnly 'com.google.code.findbugs:jsr305'

    jsw 'tanukisoft:wrapper:3.2.3'
    scanner project(path: ':sonar-scanner-engine-shaded', configuration: 'shadow')
    cyclonedx project(path: ':sonar-scanner-engine-shaded')
    web project(':server:sonar-web')
    shutdowner project(':sonar-shutdowner')

    jdbc_h2 'com.h2database:h2'
    jdbc_mssql 'com.microsoft.sqlserver:mssql-jdbc'
    jdbc_postgresql 'org.postgresql:postgresql'
}

// declare dependencies in configuration bundledPlugin to be packaged in lib/extensions
apply from: 'bundled_plugins.gradle'

//verify if sonar.properties files does not have any external input
task verifySonarProperties(type: Verify) {
  def propertiesFile = file('src/main/assembly/conf/sonar.properties')
  propertiesFile.withReader { reader ->
    def line
    while ((line = reader.readLine()) != null) {
      if (!line.startsWith('#') && !line.isEmpty()) {
        throw new GradleException('sonar.properties file by default must not provide any user configuration.')
      }
    }
  }
}

downloadLicenses {
  dependencyConfiguration = 'appLicenses'
}

task zip(type: Zip, dependsOn: [configurations.compileClasspath, tasks.downloadLicenses]) {
  duplicatesStrategy DuplicatesStrategy.EXCLUDE
  def archiveDir = "sonarqube-$project.version"

  into("${archiveDir}/") {
    from(tasks.downloadLicenses.outputs) {
      include 'dependency-license.json'
      eachFile { jsonFile ->
        def json = new JsonSlurper().parse(jsonFile.file)
        json.dependencies.each { dependency ->
          if (dependency.licenses.size() > 1) {
            def idx = dependency.licenses.findIndexOf { it.name == "Elastic License 2.0" }
            if (idx >= 0) {
              dependency.licenses = [dependency.licenses[idx]]
            }
          }
        }
        json.dependencies.sort { it.name }
        def jsonText = JsonOutput.toJson(json)
        jsonFile.file.text = JsonOutput.prettyPrint(jsonText)
      }
    }
    from(file('src/main/assembly')) {
      exclude 'conf/sonar.properties'
      exclude 'conf/wrapper.conf'
      exclude 'elasticsearch-patch'

      // elasticsearch script will be replaced by patched version below
      exclude 'elasticsearch/bin/elasticsearch'
      exclude 'elasticsearch/bin/elasticsearch-cli'
      exclude 'elasticsearch/bin/elasticsearch-keystore'
      exclude 'elasticsearch/bin/elasticsearch-node'
      exclude 'elasticsearch/bin/elasticsearch-shard'
      exclude 'elasticsearch/lib/tools/**'
      exclude 'elasticsearch/modules/aggs-matrix-stats/**'
      exclude 'elasticsearch/modules/geo/**'
      exclude 'elasticsearch/modules/ingest-common/**'
      exclude 'elasticsearch/modules/ingest-geoip/**'
      exclude 'elasticsearch/modules/ingest-user-agent/**'
      exclude 'elasticsearch/modules/kibana/**'
      exclude 'elasticsearch/modules/lang-expression/**'
      exclude 'elasticsearch/modules/lang-mustache/**'
      exclude 'elasticsearch/modules/mapper-extras/**'
      exclude 'elasticsearch/modules/rank-eval/**'
      exclude 'elasticsearch/modules/reindex/**'
      exclude 'elasticsearch/modules/repository-url/**'
      exclude 'elasticsearch/modules/tasks/**'
      exclude 'elasticsearch/modules/constant-keyword/**'
      exclude 'elasticsearch/modules/flattened/**'
      exclude 'elasticsearch/modules/frozen-indices/**'
      exclude 'elasticsearch/modules/mapper-version/**'
      exclude 'elasticsearch/modules/repositories-metering-api/**'
      exclude 'elasticsearch/modules/repository-encrypted/**'
      exclude 'elasticsearch/modules/search-business-rules/**'
      exclude 'elasticsearch/modules/searchable-snapshots/**'
      exclude 'elasticsearch/modules/snapshot-repo-test-kit/**'
      exclude 'elasticsearch/modules/spatial/**'
      exclude 'elasticsearch/modules/transform/**'
      exclude 'elasticsearch/modules/unsigned-long/**'
      exclude 'elasticsearch/modules/vectors/**'
      exclude 'elasticsearch/modules/wildcard/**'
      exclude 'elasticsearch/modules/x-pack-*/**'
    }
  }

  into("${archiveDir}/conf/") {
    from file('src/main/assembly/conf/sonar.properties')
    filter(ReplaceTokens, tokens: [
            'searchDefaultHeapSize': '512MB',
            'searchJavaOpts'       : '-Xmx512m -Xms512m -XX:MaxDirectMemorySize=256m -XX:+HeapDumpOnOutOfMemoryError',
            'ceDefaultHeapSize'    : '512MB',
            'ceJavaOpts'           : '-Xmx512m -Xms128m -XX:+HeapDumpOnOutOfMemoryError',
            'webDefaultHeapSize'   : '512MB',
            'webJavaOpts'          : '-Xmx512m -Xms128m -XX:+HeapDumpOnOutOfMemoryError'
    ])
  }
  into("${archiveDir}/conf/") {
    from file('src/main/assembly/conf/wrapper.conf')
    filter(ReplaceTokens, tokens: [
            'sqversion': version
    ])
  }

  into("${archiveDir}/elasticsearch/") {
    from file('src/main/assembly/elasticsearch-patch')
      include 'bin/elasticsearch'
  }
  // Create the empty dir (plugins) required by elasticsearch
  into("${archiveDir}/elasticsearch/") {
    from "$buildDir/elasticsearch"
  }
  into("${archiveDir}/lib/extensions/") {
    from configurations.bundledPlugin
  }
  into("${archiveDir}/lib/jsw/") {
    from configurations.jsw
  }
  into("${archiveDir}/lib/scanner/") {
    from configurations.scanner
  }
  into("${archiveDir}/lib/") {
    from shadowJar
  }
  into("${archiveDir}/web/") {
    duplicatesStrategy DuplicatesStrategy.FAIL
    // FIXME use configurations.web with correct artifacts
    from(tasks.getByPath(':server:sonar-web:yarn_run').outputs) { a ->
      if (official) {
         project(':private:branding').fileTree('src').visit { b ->
           if (!b.isDirectory()) {
             a.exclude b.relativePath.toString()
           }
         }
      }
    }
    if (official) {
      from project(':private:branding').file('src')
    }
  }
  into("${archiveDir}/lib/jdbc/mssql/") {
    from configurations.jdbc_mssql
  }
  into("${archiveDir}/lib/jdbc/postgresql/") {
    from configurations.jdbc_postgresql
  }
  into("${archiveDir}/lib/jdbc/h2/") {
    from configurations.jdbc_h2
  }
  into("${archiveDir}/lib/") {
    from configurations.shutdowner
  }
}
// Create the empty dir required by elasticsearch
zip.doFirst {
  new File(buildDir, 'elasticsearch/plugins').mkdirs()
}
// Check the size of the archive
zip.doLast {
  def minLength = 272000000
  def maxLength = 330000000

  def length = archiveFile.get().asFile.length()
  if (length < minLength)
    throw new GradleException("${archiveFileName.get()} size ($length) too small. Min is $minLength")
  if (length > maxLength)
    throw new GradleException("${destinationDirectory.get()}/${archiveFileName.get()} size ($length) too large. Max is $maxLength")
}
assemble.dependsOn zip

// the script start.sh unpacks OSS distribution into $buildDir/distributions/sonarqube-oss.
// This directory should be deleted when the zip is changed.
task cleanLocalUnzippedDir(dependsOn: zip) {
  def unzippedDir = file("$buildDir/distributions/sonarqube-$version")
  inputs.files(file("$buildDir/distributions/sonar-application-${version}.zip"))
  outputs.upToDateWhen { true }

  doLast {
    println("delete directory ${unzippedDir}")
    project.delete(unzippedDir)
  }
}
assemble.dependsOn cleanLocalUnzippedDir

artifacts { zip zip }

artifactoryPublish.skip = false

def bomFile = layout.buildDirectory.file('reports/bom.json')
cyclonedxBom {
  includeConfigs += ["runtimeClasspath", "jsw", "web", "shutdowner", "jdbc_mssql", "jdbc_postgresql", "jdbc_h2", "bundledPlugin_deps",
                     "cyclonedx"]
  outputs.file bomFile
  outputs.upToDateWhen { false }
}
def bomArtifact = artifacts.add('archives', bomFile.get().asFile) {
  type 'json'
  classifier 'cyclonedx'
  builtBy 'cyclonedxBom'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact zip
    }
    mavenJava(MavenPublication) {
      artifact bomArtifact
    }
  }
}
